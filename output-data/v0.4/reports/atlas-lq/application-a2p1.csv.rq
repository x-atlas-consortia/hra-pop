PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ASCTB-TEMP: <https://purl.org/ccf/ASCTB-TEMP_>
PREFIX CL: <http://purl.obolibrary.org/obo/CL_>
PREFIX FMA: <http://purl.org/sig/ont/fma/fma>
PREFIX UBERON: <http://purl.obolibrary.org/obo/UBERON_>
PREFIX ccf: <http://purl.org/ccf/>
PREFIX CCF: <https://purl.humanatlas.io/graph/ccf>
PREFIX HRApop: <https://purl.humanatlas.io/graph/hra-pop-lq>
PREFIX hra-pop: <https://purl.humanatlas.io/graph/hra-pop-lq#>
PREFIX dc: <http://purl.org/dc/terms/>
PREFIX hubmap: <https://entity.api.hubmapconsortium.org/entities/>
PREFIX rui: <http://purl.org/ccf/1.5/>


SELECT ?dataset (COALESCE(?reported_organ, 'Unknown') as ?reported_organ) ?tool ?modality ?as ?similarity
WHERE {
  GRAPH hra-pop:test-data {
    ?dataset ccf:has_cell_summary [
      ccf:cell_annotation_method ?tool ;
      ccf:modality ?modality ;
    ] .

    OPTIONAL { ?dataset ccf:organ_id ?reported_organ . }
  }

  GRAPH HRApop: {
    ?as ccf:has_cell_summary [
      ccf:cell_annotation_method ?tool ;
      ccf:modality ?modality ;
    ] .

    FILTER (
      STRSTARTS(STR(?as), 'http://purl.obolibrary.org/obo/') ||
      STRSTARTS(STR(?as), 'http://purl.org/sig/ont/fma/fma')
    )
  }

  GRAPH hra-pop:similarities {
    {
      [] ccf:cell_source_a ?dataset ;
          ccf:cell_source_b ?as ;
          ccf:similarity ?similarity .

      FILTER (
        STRSTARTS(STR(?as), 'http://purl.obolibrary.org/obo/') ||
        STRSTARTS(STR(?as), 'http://purl.org/sig/ont/fma/fma')
      )
    }
    UNION 
    {
      [] ccf:cell_source_a ?as ;
          ccf:cell_source_b ?dataset ;
          ccf:similarity ?similarity .

      FILTER (
        STRSTARTS(STR(?as), 'http://purl.obolibrary.org/obo/') ||
        STRSTARTS(STR(?as), 'http://purl.org/sig/ont/fma/fma')
      )
    }
  }
}
ORDER BY ?dataset
