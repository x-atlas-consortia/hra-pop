PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ASCTB-TEMP: <https://purl.org/ccf/ASCTB-TEMP_>
PREFIX CL: <http://purl.obolibrary.org/obo/CL_>
PREFIX FMA: <http://purl.org/sig/ont/fma/fma>
PREFIX UBERON: <http://purl.obolibrary.org/obo/UBERON_>
PREFIX ccf: <http://purl.org/ccf/>
PREFIX CCF: <https://purl.humanatlas.io/graph/ccf>
PREFIX HRApop: <https://purl.humanatlas.io/graph/hra-pop>
PREFIX hra-pop: <https://purl.humanatlas.io/graph/hra-pop#>
PREFIX dc: <http://purl.org/dc/terms/>
PREFIX hubmap: <https://entity.api.hubmapconsortium.org/entities/>
PREFIX rui: <http://purl.org/ccf/1.5/>

SELECT DISTINCT 
  (?datasetA as ?source)
  (?datasetB as ?target)
  (?similarity as ?weight)
  ?tool ?modality ?organ ?organId
  ?datasetA ?entityA ?datasetB ?entityB
  ?similarity ?distance
WHERE {
  GRAPH HRApop: {
    ?entityA a ccf:SpatialEntity .
    ?entityB a ccf:SpatialEntity .
    FILTER(STR(?entityA) < STR(?entityB))

    {
      ?sampleA ccf:has_registration_location ?entityA .
      ?sampleA ccf:generates_dataset ?datasetA .
    } UNION {
      ?blockA ccf:subdivided_into_sections ?sampleA .
      ?blockA ccf:has_registration_location ?entityA .
      ?sampleA ccf:generates_dataset ?datasetA .
    }

    {
      ?sampleB ccf:has_registration_location ?entityB .
      ?sampleB ccf:generates_dataset ?datasetB .
    } UNION {
      ?blockB ccf:subdivided_into_sections ?sampleB .
      ?blockB ccf:has_registration_location ?entityB .
      ?sampleB ccf:generates_dataset ?datasetB .
    }

    ?datasetA ccf:has_cell_summary [
      ccf:cell_annotation_method ?tool ;
      ccf:modality ?modality ;
    ] .

    ?datasetB ccf:has_cell_summary [
      ccf:cell_annotation_method ?tool ;
      ccf:modality ?modality ;
    ] .

    # Sample A
    [] a ccf:SpatialPlacement ;
        ccf:placement_for ?entityA ;
        ccf:placement_relative_to ?refOrgan .

    # Sample B
    [] a ccf:SpatialPlacement ;
        ccf:placement_for ?entityB ;
        ccf:placement_relative_to ?refOrgan .
  }
  
  GRAPH CCF: {
    ?refOrgan ccf:representation_of ?organIri .
    ?organIri rdfs:label ?organ .
    BIND(REPLACE(REPLACE(STR(?organIri), 'http://purl.obolibrary.org/obo/UBERON_', 'UBERON:'), 'http://purl.org/sig/ont/fma/fma', 'FMA:') as ?organId)
  }

  GRAPH hra-pop:distances {
    [] a ccf:SpatialEntityDistance;
      ccf:entity_a ?entityA ;
      ccf:entity_b ?entityB ;
      ccf:distance ?distance .
  }

  GRAPH hra-pop:similarities {
    ?edge ccf:modality ?modality ;
          ccf:cell_source_a_tool ?tool ;
          ccf:cell_source_b_tool ?tool ;
          ccf:cell_source_a ?entityA ;
          ccf:cell_source_b ?entityB ;
          ccf:similarity ?similarity .
  }
}
# ORDER BY DESC(?similarity)
