PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ASCTB-TEMP: <https://purl.org/ccf/ASCTB-TEMP_>
PREFIX CL: <http://purl.obolibrary.org/obo/CL_>
PREFIX FMA: <http://purl.org/sig/ont/fma/fma>
PREFIX UBERON: <http://purl.obolibrary.org/obo/UBERON_>
PREFIX ccf: <http://purl.org/ccf/>
PREFIX CCF: <https://purl.humanatlas.io/graph/ccf>
PREFIX HRApop: <https://purl.humanatlas.io/graph/hra-pop>
PREFIX hra-pop: <https://purl.humanatlas.io/graph/hra-pop#>
PREFIX dc: <http://purl.org/dc/terms/>
PREFIX hubmap: <https://entity.api.hubmapconsortium.org/entities/>
PREFIX rui: <http://purl.org/ccf/1.5/>

SELECT DISTINCT 
  (?datasetA as ?source)
  (?datasetB as ?target)
  (?similarity as ?weight)
  ?datasetA ?tool ?modality ?entityA ?organA ?organIdA
  ?datasetB ?tool ?modality ?entityB ?organB ?organIdB
  ?similarity ?distance
WHERE {
  GRAPH HRApop: {
    ?entityA a ccf:SpatialEntity .
    ?entityB a ccf:SpatialEntity .
    FILTER(STR(?entityA) < STR(?entityB))
  }

  GRAPH HRApop: {
    ?sampleA ccf:has_registration_location ?entityA ;
             ccf:generates_dataset ?datasetA .
    
    ?sampleB ccf:has_registration_location ?entityB ;
             ccf:generates_dataset ?datasetB .

    ?datasetA ccf:has_cell_summary [
      ccf:cell_annotation_method ?tool ;
      ccf:modality ?modality ;
    ] .

    ?datasetB ccf:has_cell_summary [
      ccf:cell_annotation_method ?tool ;
      ccf:modality ?modality ;
    ] .
  }

  GRAPH hra-pop:distances {
    [] a ccf:SpatialEntityDistance;
      ccf:entity_a ?entityA ;
      ccf:entity_b ?entityB ;
      ccf:distance ?distance .
  }

  GRAPH hra-pop:similarities {
    ?edge ccf:cell_source_a ?entityA ;
          ccf:cell_source_b ?entityB ;
          ccf:similarity ?similarity .
  }

  GRAPH HRApop: {
    # Sample A
    # ?sampleA ccf:has_registration_location ?entityA ;
    #   rdfs:label ?labelA .
    [] a ccf:SpatialPlacement ;
        ccf:placement_for ?entityA ;
        ccf:placement_relative_to ?refOrganA .

    # Sample B
    # ?sampleB ccf:has_registration_location ?entityB ;
    #   rdfs:label ?labelB .
    [] a ccf:SpatialPlacement ;
        ccf:placement_for ?entityB ;
        ccf:placement_relative_to ?refOrganB .
  }
  
  GRAPH CCF: {
    # Organ A
    ?refOrganA ccf:representation_of ?organIriA .
    ?organIriA rdfs:label ?organA .

    # Organ B
    ?refOrganB ccf:representation_of ?organIriB .
    ?organIriB rdfs:label ?organB .

    BIND(REPLACE(REPLACE(STR(?organIriA), 'http://purl.obolibrary.org/obo/UBERON_', 'UBERON:'), 'http://purl.org/sig/ont/fma/fma', 'FMA:') as ?organIdA)
    BIND(REPLACE(REPLACE(STR(?organIriB), 'http://purl.obolibrary.org/obo/UBERON_', 'UBERON:'), 'http://purl.org/sig/ont/fma/fma', 'FMA:') as ?organIdB)
  }
}
# ORDER BY DESC(?similarity)
