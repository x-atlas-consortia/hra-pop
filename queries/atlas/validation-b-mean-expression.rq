#+ summary: Validation of B mean expressions
#+ description: Atlas Datasets and their cell types and biomarkers. There is one CT-BM expression per row.

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ASCTB-TEMP: <https://purl.org/ccf/ASCTB-TEMP_>
PREFIX CL: <http://purl.obolibrary.org/obo/CL_>
PREFIX FMA: <http://purl.org/sig/ont/fma/fma>
PREFIX UBERON: <http://purl.obolibrary.org/obo/UBERON_>
PREFIX ccf: <http://purl.org/ccf/>
PREFIX HRA: <https://purl.humanatlas.io/collection/hra-api>
PREFIX HRApop: <https://purl.humanatlas.io/graph/hra-pop>

SELECT DISTINCT
  ?tool
  ?modality
  ?organ_id
  ?organ_label
  ?dataset_id
  ?cell_id
  ?cell_label
  ?gene_label
  ?mean_gene_expression_value
FROM HRApop:
FROM HRA:
WHERE {
  ?dataset_id rdf:type ccf:Dataset ;
    ccf:organ_id ?organ_iri ;
    ccf:has_cell_summary [
    ccf:cell_annotation_method ?tool ;
    ccf:modality ?modality ;
    ccf:has_cell_summary_row [
      ccf:cell_id ?cell_iri ;
      ccf:cell_label ?cell_label ;
      ccf:gene_expr [
        ccf:gene_label ?gene_label ;
        ccf:mean_gene_expr_value ?mean_gene_expression_value ;
      ]
    ]
  ] .

  ?organ_iri rdfs:label ?organ_label .
  BIND(REPLACE(REPLACE(STR(?organ_iri), STR(UBERON:), 'UBERON:'), STR(FMA:), 'FMA:') as ?organ_id)
  BIND(REPLACE(REPLACE(STR(?cell_iri), STR(CL:), 'CL:'), STR(ASCTB-TEMP:), 'ASCTB-TEMP:') as ?cell_id)
}
