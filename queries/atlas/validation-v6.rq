#+ summary: Validation V6
#+ description:

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ASCTB-TEMP: <https://purl.org/ccf/ASCTB-TEMP_>
PREFIX CL: <http://purl.obolibrary.org/obo/CL_>
PREFIX FMA: <http://purl.org/sig/ont/fma/fma>
PREFIX UBERON: <http://purl.obolibrary.org/obo/UBERON_>
PREFIX ccf: <http://purl.org/ccf/>
PREFIX CCF: <https://purl.humanatlas.io/graph/ccf>
PREFIX HRApop: <https://purl.humanatlas.io/graph/hra-pop>
PREFIX hra-pop: <https://purl.humanatlas.io/graph/hra-pop#>
PREFIX dc: <http://purl.org/dc/terms/>
PREFIX hubmap: <https://entity.api.hubmapconsortium.org/entities/>
PREFIX rui: <http://purl.org/ccf/1.5/>

SELECT DISTINCT
  ?sex
  ?tool
  ?modality
  ?organ
  ?organId
  ?as
  ?as_id
  ?cell_id
  ?cell_label
  ?cell_count
  ?percentage
  ?asct_relation_in_asctb_table
  ?indirect_asct_relation_in_asctb_table
FROM HRApop:
FROM CCF:
WHERE {
  hint:Query hint:analytic "true" .

  ?as3d_id
    ccf:has_reference_organ ?ref_organ ;
    ccf:representation_of ?as_iri .

  ?as_iri ccf:has_cell_summary [
    ccf:sex ?sex ;
    ccf:cell_annotation_method ?tool ;
    ccf:modality ?modality ;
    ccf:has_cell_summary_row [
      ccf:cell_id ?cell_iri ;
      ccf:cell_label ?cell_label ;
      ccf:cell_count ?cell_count ;
      ccf:percentage_of_total ?percentage
    ]
  ] .

  BIND(REPLACE(REPLACE(STR(?cell_iri), STR(CL:), 'CL:'), STR(ASCTB-TEMP:), 'ASCTB-TEMP:') as ?cell_id)

  ?ref_organ ccf:representation_of ?organIri .
  ?organIri rdfs:label ?organ .
  BIND(REPLACE(REPLACE(STR(?organIri), STR(UBERON:), 'UBERON:'), STR(FMA:), 'FMA:') as ?organId)

  ?as_iri rdfs:label ?as .
  BIND(REPLACE(REPLACE(STR(?as_iri), STR(UBERON:), 'UBERON:'), STR(FMA:), 'FMA:') as ?as_id)

  OPTIONAL {
    ?as_iri ccf:has_cell_summary [] .
    ?as_child_iri ccf:ccf_part_of* ?as_iri .
    FILTER(?as_iri != ?as_child_iri)
    hint:SubQuery hint:runOnce true .
  }

  BIND(EXISTS { 
    ?cell_iri ccf:ccf_located_in ?as_iri .
  } as ?asct_relation_in_asctb_table)

  BIND(EXISTS {
    ?cell_iri ccf:ccf_located_in ?as_child_iri .
  } as ?indirect_asct_relation_in_asctb_table)
}
