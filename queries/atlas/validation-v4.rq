#+ summary: Validation V4
#+ description:

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ASCTB-TEMP: <https://purl.org/ccf/ASCTB-TEMP_>
PREFIX CL: <http://purl.obolibrary.org/obo/CL_>
PREFIX FMA: <http://purl.org/sig/ont/fma/fma>
PREFIX UBERON: <http://purl.obolibrary.org/obo/UBERON_>
PREFIX ccf: <http://purl.org/ccf/>
PREFIX CCF: <https://purl.humanatlas.io/graph/ccf>
PREFIX HRApop: <https://purl.humanatlas.io/graph/hra-pop>
PREFIX hra-pop: <https://purl.humanatlas.io/graph/hra-pop#>
PREFIX dc: <http://purl.org/dc/terms/>
PREFIX hubmap: <https://entity.api.hubmapconsortium.org/entities/>
PREFIX rui: <http://purl.org/ccf/1.5/>

SELECT
  ?sex
  ?tool 
  ?modality
  (COUNT(DISTINCT(?asctb)) as ?asctb_only) 
  (COUNT(DISTINCT(?both)) as ?both) 
  (COUNT(DISTINCT(?exp)) as ?exp_only)
WHERE {
  # Get CTs only in ASCT+B
  {
    SELECT DISTINCT ?sex ?tool ?modality ?asctb {
      GRAPH CCF: {
        ?asctb ccf:ccf_asctb_type ?type .
      }
      GRAPH HRApop: {
        [] ccf:has_cell_summary [
          ccf:sex ?sex ;
          ccf:cell_annotation_method ?tool ;
          ccf:modality ?modality ;
        ]
      }
      FILTER (?type = 'CT')
      FILTER NOT EXISTS {
        GRAPH HRApop: {
          [] ccf:has_cell_summary [
            ccf:sex ?sex ;
            ccf:cell_annotation_method ?tool ;
            ccf:modality ?modality ;
            ccf:has_cell_summary_row [
              ccf:cell_id ?asctb ;
            ]
          ] .
        }
      }
    }
  }

  # Get CTs only in Experimental Data
  {
    SELECT DISTINCT ?sex ?tool ?modality ?exp {
      GRAPH HRApop: {
        [] ccf:has_cell_summary [
          ccf:sex ?sex ;
          ccf:cell_annotation_method ?tool ;
          ccf:modality ?modality ;
          ccf:has_cell_summary_row [
            ccf:cell_id ?exp ;
          ]
        ] .
      }
      FILTER NOT EXISTS {
        GRAPH CCF:{
          ?exp ccf:ccf_asctb_type ?type .
        }
        FILTER (?type = 'CT')
      }
    }
  }

  # Get CTs in Both
  {
    SELECT DISTINCT ?sex ?tool ?modality ?both {
      GRAPH CCF: {
        ?both ccf:ccf_asctb_type ?type .
      }
      GRAPH HRApop: {
        [] ccf:has_cell_summary [
          ccf:sex ?sex ;
          ccf:cell_annotation_method ?tool ;
          ccf:modality ?modality ;
        ]
      }
      FILTER (?type = 'CT')
      FILTER EXISTS {
        SELECT ?sex ?tool ?modality ?both {
          GRAPH HRApop: {
            [] ccf:has_cell_summary [
              ccf:sex ?sex ;
              ccf:cell_annotation_method ?tool ;
              ccf:modality ?modality ;
              ccf:has_cell_summary_row [
                ccf:cell_id ?both ;
              ]
            ] .
          }
        }
      }
    }
  }
}
GROUP BY ?sex ?tool ?modality