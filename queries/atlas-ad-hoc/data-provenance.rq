PREFIX ccf: <http://purl.org/ccf/>
PREFIX HRA: <https://purl.humanatlas.io/collection/hra-api>
PREFIX HRApop: <https://purl.humanatlas.io/graph/hra-pop>
PREFIX HRApopFull: <https://purl.humanatlas.io/ds-graph/hra-pop-full>
PREFIX HRApopTestData: <https://purl.humanatlas.io/graph/hra-pop#test-data>

SELECT ?label ?count
FROM HRA:
FROM HRApop:
WHERE {
  {
    SELECT ("HuBMAP datasets" as ?label) (COUNT(DISTINCT ?dataset) as ?count) (1 as ?order)
    WHERE {
      ?dataset a ccf:Dataset .
      FILTER(STRSTARTS(STR(?dataset), "https://entity.api.hubmapconsortium.org/entities/"))
    }
  }
  UNION
  {
    SELECT ("SenNet datasets" as ?label) (COUNT(DISTINCT ?dataset) as ?count) (2 as ?order)
    WHERE {
      ?dataset a ccf:Dataset .
      FILTER(STRSTARTS(STR(?dataset), "https://entity.api.sennetconsortium.org/entities/"))
    }
  }
  UNION
  {
    SELECT ("tissue donors" as ?label) (COUNT(DISTINCT ?donor) as ?count) (3 as ?order)
    WHERE {
      ?donor a ccf:Donor .
    }
  }
  UNION
  {
    SELECT ("organs" as ?label) (COUNT(DISTINCT ?organ) as ?count) (4 as ?order)
    WHERE {
      ?as ccf:has_cell_summary [] .
      [] ccf:representation_of ?as ;
        ccf:has_reference_organ [
          ccf:representation_of ?organ ;
        ] .
    }
  }
  UNION
  {
    SELECT ("anatomical structures" as ?label) (COUNT(DISTINCT ?as) as ?count) (5 as ?order)
    WHERE {
      ?as ccf:has_cell_summary [] .
      [] ccf:representation_of ?as .
    }
  }
  UNION
  {
    SELECT ("cells" as ?label) (SUM(?cell_count) as ?count) (6 as ?order)
    WHERE {
      [] a ccf:Dataset ;
         ccf:has_cell_summary [
            ccf:has_cell_summary_row [
              ccf:cell_count ?cell_count ;
            ] ;
         ] .
    }
  }
}
ORDER BY ?order

